plugins {
	id 'com.diffplug.spotless' version '5.6.1'
	id 'java-gradle-plugin'
	id "com.gradle.plugin-publish" version "0.12.0"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories { jcenter() }

configurations { pluginDependency }

dependencies {
	compile gradleApi()
	compile localGroovy()

	testCompile gradleTestKit()
	testCompile 'junit:junit:4+'
	testCompile "com.vladsch.flexmark:flexmark-all:$flexmark_version"

	pluginDependency "com.vladsch.flexmark:flexmark:$flexmark_version"
	pluginDependency "com.vladsch.flexmark:flexmark-pdf-converter:$flexmark_version"

	// Adds pluginDependency to gradle plugin jar
	configurations.compile.extendsFrom(configurations.pluginDependency)
}

sourceSets {
	main {
		java { srcDir 'src/main/java' }
		groovy { srcDir 'src/main/groovy' }
		resources { srcDir 'src/main/resources' }
	}
	test {
		java { srcDir 'src/test/java' }
		groovy { srcDir 'src/test/groovy' }
		resources { srcDir 'src/test/resources' }
	}
}

spotless {
	java { googleJavaFormat() }
	groovy { greclipse() }
	groovyGradle { greclipse() }

	lineEndings = com.diffplug.spotless.LineEnding.UNIX
}

jar {
	from {
		configurations.pluginDependency.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

gradlePlugin {
	plugins {
		markdownToPdfPlugin {
			id = 'com.appiancorp.cs.markdown-to-pdf'
			implementationClass = 'de.fntsoftware.gradle.MarkdownToPdfPlugin'
		}
	}
}

pluginBundle {
	website = 'https://www.fntsoftware.com/'
	vcsUrl = 'https://github.com/mchirlin/gradle-plugin-markdown2pdf'
	description = 'Plugin to create PDF from MD based on the flexmark plugin'
	tags = [
		'markdowntopdf',
		'markdown2pdf',
		'markdown'
	]

	plugins {
		markdownToPdfPlugin {
			displayName = 'MarkdownToPdf Plugin'
		}
	}
}